#include "MAPL_Exceptions.h"
#include "MAPL_ErrLog.h"
module MAPL_ExtDataFileStream
   use ESMF
   use yaFyaml
   use MAPL_KeywordEnforcerMod
   use MAPL_ExceptionHandling
   use MAPL_TimeStringConversion
   use MAPL_ExtDataCollectionMod
   use MAPL_CollectionVectorMod
   use MAPL_ExtDataCollectionManagerMod
   implicit none
   private

   type, public :: ExtDataFileStream
      character(:), allocatable :: file_template
      type(ESMF_TimeInterval) :: frequency
      type(ESMF_Time) :: reff_time
      integer :: collection_id
      integer, allocatable :: valid_range(:)
   end type

   interface ExtDataFileStream
      module procedure new_ExtDataFileStream_from_yaml
   end interface

contains

   function new_ExtDataFileStream_from_yaml(config,current_time,unusable,rc) result(data_set)
      type(Configuration), intent(in) :: config
      type(ESMF_Time), intent(in) :: current_time
      class(KeywordEnforcer), optional, intent(in) :: unusable
      integer, optional, intent(out) :: rc

      type(ExtDataFileStream), target :: data_set
      integer :: status
      integer :: last_token
      integer :: iyy,imm,idd,ihh,imn,isc
      character(len=2) :: token
      character(len=:), allocatable :: file_frequency, file_reff_time
      logical :: is_present

      _UNUSED_DUMMY(unusable)

      call config%get(data_set%file_template,"file_template",default='',is_present=is_present,rc=status)
      _VERIFY(status)
      call config%get(file_frequency,"file_frequency",default='',rc=status)
      _VERIFY(status)
      if (file_frequency /= '') then
         data_set%frequency = string_to_esmf_timeinterval(file_frequency)
      else
         last_token = index(data_set%file_template,'%',back=.true.)
         if (last_token.gt.0) then
            token = data_set%file_template(last_token+1:last_token+2)
            select case(token)
            case("y4")
               call ESMF_TimeIntervalSet(data_set%frequency,yy=1,__RC__)
            case("m2")
               call ESMF_TimeIntervalSet(data_set%frequency,mm=1,__RC__)
            case("d2")
               call ESMF_TimeIntervalSet(data_set%frequency,d=1,__RC__)
            case("h2")
               call ESMF_TimeIntervalSet(data_set%frequency,h=1,__RC__)
            case("n2")
               call ESMF_TimeIntervalSet(data_set%frequency,m=1,__RC__)
            end select
         else
            ! couldn't find any tokens so all the data must be on one file
            call ESMF_TimeIntervalSet(data_set%frequency,__RC__)
         end if
      end if

      call config%get(file_reff_time,"file_reference_time",default='',rc=status)
      _VERIFY(status)
      if (file_reff_time /= '') then
         data_set%reff_time = string_to_esmf_time(file_reff_time)
      else
         last_token = index(data_set%file_template,'%',back=.true.)
         if (last_token.gt.0) then
            call ESMF_TimeGet(current_time, yy=iyy, mm=imm, dd=idd,h=ihh, m=imn, s=isc  ,__RC__)
            token = data_set%file_template(last_token+1:last_token+2)
            select case(token)
            case("y4")
               call ESMF_TimeSet(data_set%reff_time,yy=iyy,mm=1,dd=1,h=0,m=0,s=0,__RC__)
            case("m2")
               call ESMF_TimeSet(data_set%reff_time,yy=iyy,mm=imm,dd=1,h=0,m=0,s=0,__RC__)
            case("d2")
               call ESMF_TimeSet(data_set%reff_time,yy=iyy,mm=imm,dd=idd,h=0,m=0,s=0,__RC__)
            case("h2")
               call ESMF_TimeSet(data_set%reff_time,yy=iyy,mm=imm,dd=idd,h=ihh,m=0,s=0,__RC__)
            case("n2")
               call ESMF_TimeSet(data_set%reff_time,yy=iyy,mm=imm,dd=idd,h=ihh,m=imn,s=0,__RC__)
            end select
         else
            data_set%reff_time = current_time
         end if
      end if

      data_set%valid_range = config%at("valid_range")
      data_set%collection_id = MAPL_ExtDataAddCollection(data_set%file_template)

      _RETURN(_SUCCESS)

   end function new_ExtDataFileStream_from_yaml

end module MAPL_ExtDataFileStream

module MAPL_ExtDataFileStreamMap
   use MAPL_ExtDataFileStream

#include "types/key_deferredLengthString.inc"
#define _value type(ExtDataFileStream)
#define _alt

#define _map ExtDataFileStreamMap
#define _iterator ExtDataFileStreamMapIterator

#include "templates/map.inc"

#undef _iterator
#undef _map

#undef _alt
#undef _value

end module MAPL_ExtDataFileStreamMap
